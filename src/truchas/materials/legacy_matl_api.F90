#include "f90_assert.fpp"

module legacy_matl_api

  use,intrinsic :: iso_fortran_env, only: r8 => real64
  use legacy_matl_type
  implicit none
  private

  public :: matl_init, matl_free
  public :: gather_vof, matl_get_vof, matl_get_cell_vof, define_matl
  public :: read_matl_data

  integer, public, protected :: nmat
  type(legacy_matl), allocatable :: this

contains

  subroutine matl_init(ncell)
    use material_model_driver, only: matl_model
    integer, intent(in) :: ncell
    allocate(this)
    nmat = matl_model%nphase
    call this%init(nmat, ncell)
  end subroutine

  subroutine matl_free
    deallocate(this)
  end subroutine

  subroutine gather_vof(m, vof)
    integer, intent(in) :: m
    real(r8), intent(out) :: vof(:)
    call this%gather_vof(m, vof)
  end subroutine

  subroutine matl_get_vof(vof)
    real(r8), intent(inout) :: vof(:,:)
    call this%get_vof(vof)
  end subroutine

  subroutine matl_get_cell_vof(m, vof)
    integer, intent(in) :: m
    real(r8), intent(out) :: vof(:)
    call this%get_cell_vof(m, vof)
  end subroutine

  subroutine define_matl(vof)
    real(r8), intent(in) :: vof(:,:)
    call this%set_vof(vof)
  end subroutine

  !! This subroutine reads the volume fraction data from a restart file opened
  !! (and pre-positioned) on UNIT, and uses this data (properly distributed and
  !! permuted) to define the module structure MATL.  VERSION is the version
  !! number of the restart file format.
  !!
  !! NB: It is implicitly assumed that the material indices in the restart file
  !! directly correspond to the material indices generated by the input file.
  !! We require that the number of materials agree between the input and restart
  !! files; this constraint could probably be relaxed to allow the input to
  !! specify more (new additional) materials.

  subroutine read_matl_data (unit, version)

    use restart_utilities, only: read_var, read_dist_array, halt
    use string_utilities, only: i_to_c
    use base_mesh_class
    use mesh_manager, only: named_mesh_ptr

    integer, intent(in) :: unit, version

    integer :: n
    real(r8), allocatable :: vf(:,:)
    class(base_mesh), pointer :: mesh

    mesh => named_mesh_ptr('MAIN')
    INSIST(associated(mesh))

    !! Read the number of materials defined in the restart file.
    call read_var (unit, n, 'READ_MATL_DATA: error reading NMAT record')
    if (n /= nmat) call halt ('READ_MATL_DATA: incompatible NMAT value: ' // i_to_c(n))

    !! Read the volume fraction array.
    allocate(vf(n,mesh%ncell_onP))
    call read_dist_array (unit, vf, mesh%xcell(:mesh%ncell_onP), 'READ_MATL_DATA: error reading VF records')

    !! Derive the MATL structure from the volume fraction array.
    call define_matl (vf)
    deallocate(vf)

  end subroutine read_matl_data

end module legacy_matl_api
