!!
!! IH_SOURCE_FUNC_TYPE
!!
!! This module defines an extension of the VECTOR_FUNC class that implements
!! the external magnetic field source profile for induction heating simulations.
!! The space-dependent profile is the superposition of a constant background
!! field and the magnetic field generated by current in a set of induction
!! coils. The background field and coil axes are restricted to having a common
!! orientation, and that orientation must be one of the three coordinate axes.
!!
!! Neil Carlson <neil.n.carlson@gmail.com>
!! Refactored February 2024
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! This file is part of Truchas. 3-Clause BSD license; see the LICENSE file.
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

#include "f90_assert.fpp"

module ih_source_func_type

  use,intrinsic :: iso_fortran_env, only: r8 => real64
  use vector_func_class
  implicit none
  private

  type, extends(vector_func), public :: ih_source_func
    private
    type(induction_coil), allocatable :: coil(:)
    real(r8), allocatable :: current(:)
    real(r8) :: const, scf = 1.0_r8
    integer :: shift
  contains
    procedure :: init
    procedure :: eval
    procedure :: eval_comp
  end type

  type, public :: induction_coil
    real(r8) :: radius, length, center(3)!, axis(3)
    integer :: nloop
  end type

contains

  subroutine init(this, axis, const, coil, current, scf)

    class(ih_source_func), intent(out) :: this
    character, intent(in) :: axis
    real(r8), intent(in) :: const
    type(induction_coil), intent(in) :: coil(:)
    real(r8), intent(in) :: current(:)
    real(r8), intent(in), optional :: scf

    ASSERT(size(coil) == size(current))

    this%dim = 3

    this%const = const
    this%coil = coil
    this%current = current
    if (present(scf)) this%scf = scf

    !! We will compute in a frame of reference where the z-axis is the symmetry
    !! axis. The spatial coordinate is rotated to take the symmetry axis to the
    !! z-axis using CSHIFT with the shift set here (and the resulting source
    !! vector rotated back using the opposite.)

    select case (axis)
    case ('x','X')
      this%shift = 1
    case ('y','Y')
      this%shift = -1
    case ('z','Z')
      this%shift = 0
    case default
      INSIST(.false.)
    end select

    !TODO: allow for a general symmetry axis orientation?

  end subroutine

  function eval(this, x) result(fx)

    use solenoid_fields, only: H_coil

    class(ih_source_func), intent(in) :: this
    real(r8), intent(in) :: x(:)
    real(r8) :: fx(this%dim)

    integer :: i
    real(r8) :: y(3)

    !! Rotate to a frame where the symmetry axis is the z-axis
    if (this%shift == 0) then
      y = x ! no rotation needed
    else
      y = cshift(x,shift=this%shift)
    end if

    !! A constant field directed along the symmetry axis
    fx = 0.0_r8
    fx(3) = this%const

    !! Superimpose the fields due to the coils
    !FIXME! I think coil%center should be rotated!
    do i = 1, size(this%coil)
      associate(coil => this%coil(i))
        fx = fx + this%current(i) * H_coil(y-coil%center, coil%radius, coil%length/2, coil%nloop)
      end associate
    end do
    fx = this%scf*fx

    !! Rotate the computed field back to the original frame
    if (this%shift /= 0) fx = cshift(fx,shift=-this%shift)

  end function

  ! Not implemented
  function eval_comp(this, i, x) result(fx)
    class(ih_source_func), intent(in) :: this
    integer, intent(in) :: i
    real(r8), intent(in) :: x(:)
    real(r8) :: fx
    INSIST(.false.)
    fx = 0
  end function

end module ih_source_func_type
